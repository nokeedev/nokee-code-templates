plugins {
	id 'java-library'
	id 'java-gradle-plugin'
	id 'maven-publish'
}

group = 'dev.nokee.templates'
version = '1.0-SNAPSHOT'

repositories {
	mavenCentral()
}

dependencies {
	api 'dev.gradleplugins:gradle-fixtures-source-elements:2.0-milestone-1'
	api 'dev.gradleplugins:elements:2.0-milestone-1'
}

java {
	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11
}

gradlePlugin {
	plugins {
		templates {
			id = "nokeebuild.code-templates"
			implementationClass = "dev.nokee.templates.plugins.CodeTemplatesPlugin"
		}
	}
}

publishing {
	publications {
		java(MavenPublication) {
			from components.java
		}
	}
}

tasks.named('check') {
	dependsOn {
		return gradle.includedBuilds.findAll { it.name.startsWith('templates-') }.collect { includedBuild ->
			return tasks.register("assemble${includedBuild.name.capitalize()}", GradleBuild) { task ->
				task.dir = includedBuild.projectDir
				task.tasks = ['assemble']
			}
		}
	}
}

// Configure source-elements annotation processor
sourceSets.main { SourceSet sourceSet ->
	dependencies.add(sourceSet.annotationProcessorConfigurationName, 'dev.gradleplugins:source-elements-annotation-processor:latest.release')

	tasks.named(sourceSet.compileJavaTaskName, JavaCompile) { task ->
		task.options.compilerArgs.add("-AbasePath=" + project.getProjectDir())
	}
}
